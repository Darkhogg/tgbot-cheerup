# Maintainer: Somebody <somebody[at]foo[dot]tld>
pkgname=tgbot-cheerup
pkgver=r5.ab62011
pkgrel=1
pkgdesc="Telegram bot that sends cheering up messages at specific intervals"
url="http://www.foo.tld"
arch=('any')
license=('MIT')
depends=('nodejs' 'mongodb' 'nodejs-foreman')
makedepends=('npm')
source=("$pkgname"::git+https://github.com/Darkhogg/tgbot-cheerup.git
        tgbot-cheerup.service)
sha1sums=('SKIP'
          '33dd090f98c32f7f7258122f701658bed13f2f07')

pkgver() {
    cd "$srcdir"/"$pkgname"

    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "$srcdir"/"$pkgname"

    msg2 'Installing npm dependencies'
    npm install

    msg2 'Deduping packages'
    npm dedupe
}

package() {
    # install in /opt/tgbot-cheerup
    install -dm755 "$pkgdir"/opt/tgbot-cheerup
    cp -r "$srcdir"/"$pkgname"/* "$pkgdir"/opt/tgbot-cheerup
    rm -rf "$pkgdir"/opt/tgbot-cheerup/arch

    # Move the .service to its dir
    install -Dm644 "$srcdir"/tgbot-cheerup.service "$pkgdir"/usr/lib/systemd/system/tgbot-cheerup.service
}
