# Maintainer: Somebody <somebody[at]foo[dot]tld>
pkgname=tgbot-cheerup
pkgver=latest
pkgrel=1
pkgdesc="Telegram bot that sends cheering up messages at specific intervals"
url="http://www.foo.tld"
arch=('any')
license=('MIT')
depends=('nodejs' 'mongodb' 'nodejs-foreman')
makedepends=('npm')
backup=(etc/tgbot-cheerup.env)
source=("$pkgname"::git+https://github.com/Darkhogg/tgbot-cheerup.git
        tgbot-cheerup.service
        tgbot-cheerup.env)
sha1sums=('SKIP'
          '2f40a643ee7a7724fe10f096d26b5d11f9f6b855'
          'a9992d7ba6c50c632dde5e600886bf68cb187704')

pkgver() {
    cd "$srcdir"/"$pkgname"

    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "$srcdir"/"$pkgname"

    msg2 'Installing npm dependencies'
    npm install

    msg2 'Deduping packages'
    npm dedupe
}

package() {
    # install in /opt/tgbot-cheerup
    install -dm755 "$pkgdir"/opt/tgbot-cheerup
    cp -r "$srcdir"/"$pkgname"/* "$pkgdir"/opt/tgbot-cheerup
    rm -rf "$pkgdir"/opt/tgbot-cheerup/arch

    install -Dm644 "$srcdir"/tgbot-cheerup.service "$pkgdir"/usr/lib/systemd/system/tgbot-cheerup.service
    install -Dm600 "$srcdir"/tgbot-cheerup.env "$pkgdir"/etc/tgbot-cheerup.env
}
